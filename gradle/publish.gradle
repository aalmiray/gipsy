/*
 * Copyright 2013-2018 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import java.text.SimpleDateFormat

Date buildTimeAndDate = new Date()
ext {   
    buildDate = new SimpleDateFormat('yyyy-MM-dd').format(buildTimeAndDate)
    buildTime = new SimpleDateFormat('HH:mm:ss.SSSZ').format(buildTimeAndDate)
}

apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

jar {
    manifest {
        attributes(
            'Built-By': System.properties['user.name'],
            'Created-By': "Gradle ${gradle.gradleVersion}",
            'x-Build-Jdk': System.properties['java.version'] + ' (' + System.properties['java.vendor'] + ' ' + System.properties["java.vm.version"] + ')',
            'x-Build-Date': project.buildDate,
            'x-Build-Time': project.buildTime,
            'x-Build-Revision': versioning.info.commit,
            'Specification-Title': project.name,
            'Specification-Version': project.version,
            'Specification-Vendor': 'Kordamp',
            'Implementation-Title': project.name,
            'Implementation-Version': project.version,
            'Implementation-Vendor': 'Kordamp',
            'Automatic-Module-Name': 'org.kordamp.gipsy'
        )
    }
}

publishing {
    publications {
        mavenCustom(MavenPublication) {
            from components.java
            artifact sourceJar
            artifact javadocJar

            pom {
                name = project.name
                description = 'Groovy version of Jipsy, a configurable AST Transformation to simplify the use of the Service Provider Interface.'
                url = 'http://github.com/aalmiray/gipsy'
                inceptionYear = '2013'
                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }
                scm {
                    url = 'https://github.com/aalmiray/jipsy.git'
                }
                developers {
                    developer {
                        id = 'aalmiray'
                        name = 'Andres Almiray'
                    }
                }
            }
        }
    }
}

task install(dependsOn: publishToMavenLocal)

if (!project.hasProperty('bintrayUsername')) ext.bintrayUsername = ''
if (!project.hasProperty('bintrayApiKey')) ext.bintrayApiKey = ''
if (!project.hasProperty('mavenUsername')) ext.mavenUsername = ''
if (!project.hasProperty('mavenPassword')) ext.mavenPassword = ''

bintray {
    user = project.bintrayUsername
    key = project.bintrayApiKey
    publications = ['mavenCustom']
    pkg {
        repo = 'kordamp'
        userOrg = 'aalmiray'
        name = 'gipsy'
        desc = 'Groovy version of Jipsy, a configurable AST Transformation to simplify the use of the Service Provider Interface.'
        licenses = ['Apache-2.0']
        labels = ['groovy', 'jipsy', 'gipsy', 'annotation', 'processor']
        websiteUrl = 'https://github.com/aalmiray/gipsy'
        issueTrackerUrl = 'https://github.com/aalmiray/gipsy/issues'
        vcsUrl = 'https://github.com/aalmiray/gipsy.git'
        publicDownloadNumbers = true
        githubRepo = 'aalmiray/gipsy'
        version {
            name = project.version
            vcsTag = "${project.name}-${project.version}"
            mavenCentralSync {
                sync = true
                user = project.mavenUsername
                password = project.mavenPassword
            }
        }
    }
}
