/*
 * Copyright 2010-2014 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import java.text.SimpleDateFormat

apply plugin: 'maven-publish'

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom.withXml {
                asNode().with {
                    appendNode('name', 'gipsy')
                    appendNode('description',
                        'Groovy version of the Jipsy Annotation Processor')
                    appendNode('url', 'http://github.com/aalmiray/gipsy')
                    appendNode('inceptionYear', '2013')
                    appendNode('licenses')
                        .appendNode('license')
                        .appendNode('name', 'The Apache Software License, Version 2.0').parent()
                        .appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt').parent()
                        .appendNode('distribution', 'repo')
                    appendNode('scm')
                        .appendNode('url', 'https://github.com/aalmiray/gipsy')
                    Node developers = appendNode('developers')
                    [
                        aalmiray: 'Andres Almiray',
                    ].each { devId, devName ->
                        developers.appendNode('developer')
                            .appendNode('id', devId).parent()
                            .appendNode('name', devName).parent()
                            .appendNode('roles')
                            .appendNode('role', 'Developer')
                    }
                }
            }

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

Date buildTimeAndDate = new Date()
ext {
    buildTime = new SimpleDateFormat('dd-MMM-yyyy').format(buildTimeAndDate)
    buildDate = new SimpleDateFormat('hh:mm aa').format(buildTimeAndDate)
}

jar {
    manifest {
        attributes(
            'Built-By': System.properties['user.name'],
            'Created-By': System.properties['java.version'] + " (" + System.properties['java.vendor'] + " " + System.getProperty("java.vm.version") + ")",
            'Build-Date': buildTime,
            'Build-Time': buildDate,
            'Specification-Title': project.name,
            'Specification-Version': project.version,
            'Specification-Vendor': 'Kordamp',
            'Implementation-Title': project.name,
            'Implementation-Version': project.version,
            'Implementation-Vendor': 'Kordamp'
        )
    }
}