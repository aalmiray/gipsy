/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Copyright 2013-2019 Andres Almiray
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id 'java'
    id 'org.kordamp.gradle.kordamp-parentpom' version '1.9.1'
}

config {
    info {
        name          = 'Gipsy'
        description   = 'Groovy version of Jipsy, a configurable AST Transformation to simplify the use of the Service Provider Interface.'
        inceptionYear = '2013'
        tags          = ['groovy', 'jipsy', 'gipsy', 'annotation', 'processor']

        specification  { enabled = false }
    }

    javadoc {
        autoLinks {
            enabled = false
        }
        options {
            links 'https://raw.githubusercontent.com/aalmiray/jipsy/gh-pages/apidocs/'
        }
    }
}

allprojects {
    apply plugin: 'idea'
}

idea {
    project {
        jdkName '1.8'
        languageLevel '1.8'

        ipr {
            withXml { provider ->
                def node = provider.asNode()
                node.component.find { it.'@name' == 'VcsDirectoryMappings' }?.mapping[0].'@vcs' = 'Git'
            }
        }
    }
}

dependencies {
    compile "org.kordamp.jipsy:jipsy:${project.version}"
    compileOnly "org.codehaus.groovy:groovy:${groovyVersion}"
    testCompile "org.codehaus.groovy:groovy:${groovyVersion}"
    testCompile 'junit:junit:4.12'
}

compileJava {
    doFirst {
        options.compilerArgs = ['-proc:none']
    }
}

jar {
    manifest {
        attributes('Automatic-Module-Name': 'org.kordamp.gipsy')
    }
}
